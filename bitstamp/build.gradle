apply plugin : 'application'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
}

mainClassName = 'com.blokaly.ceres.bitstamp.BitstampApp'
applicationName = "bitstampfh"
version = 1.0

dependencies {
    compile project(':core')
    compile "com.pusher:pusher-java-client:1.8.0"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "com.google.code.gson:gson:2.8.2"
    compile 'org.apache.kafka:kafka-clients:1.0.0'

    testCompile "info.cukes:cucumber-java8:1.2.5"
    testCompile "info.cukes:cucumber-junit:1.2.5"
}

// conf files should be under src/conf folder, avoid duplication
sourceSets {
    main {
        resources {
            exclude '**/*'
        }
    }
}

applicationDefaultJvmArgs = ["-Dlog.root=MY_APP_HOME/logs", "-Dlog.name=${applicationName}"]

applicationDistribution.from("src/conf") { into "conf" }

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
        unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
        windowsScript.text = windowsScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
    }
}