subprojects {
    apply plugin : 'application'
    apply plugin: 'com.bmuschko.docker-java-application'

    jar {
        exclude('logback.xml')
        exclude('defaults.conf')
    }

    applicationDistribution.from("src/conf") { into "conf" }
    ext.memoryUsage = "512m"
    ext.getMemoryUsage = {memoryUsage}

    applicationDefaultJvmArgs = [
            "-server", "-showversion",
            "-XmsMY_APP_MEM",
            "-XmxMY_APP_MEM",
            "-Dlog.root=MY_APP_HOME/logs",
            "-Dlog.name=MY_APP_NAME",
            "-Duser.timezone=GMT",
            "-Dstd.redirect=true",
            "-XX:-OmitStackTraceInFastThrow",
            "-XX:+HeapDumpOnOutOfMemoryError",
            "-XX:HeapDumpPath=MY_APP_HOME/logs/gc.hprof",
            "-XX:+DisableExplicitGC",
            "-XX:+PrintGCDetails",
            "-XX:+PrintGCApplicationStoppedTime",
            "-XX:+PrintGCApplicationConcurrentTime",
            "-XX:+PrintGCDateStamps",
            "-XX:+UseConcMarkSweepGC",
            "-XX:+ScavengeBeforeFullGC",
            "-XX:CMSInitiatingOccupancyFraction=55",
            "-XX:+CMSScavengeBeforeRemark",
            "-XX:-CMSIncrementalMode",
            "-XX:-CMSClassUnloadingEnabled",
            "-XX:+CMSConcurrentMTEnabled",
            "-XX:+CMSParallelRemarkEnabled",
            "-XX:+UseStringCache",
            "-XX:+OptimizeStringConcat",
            "-Xloggc:MY_APP_HOME/logs/gc.log"
    ]

    startScripts {
        doLast {
            unixScript.text = unixScript.text.replace('MY_APP_NAME', '\$APP_NAME')
            unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
            unixScript.text = unixScript.text.replace('MY_APP_MEM', getMemoryUsage())
            unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
            windowsScript.text = windowsScript.text.replace('MY_APP_NAME', '\$APP_NAME')
            windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
            windowsScript.text = windowsScript.text.replace('MY_APP_MEM', getMemoryUsage())
            windowsScript.text = windowsScript.text.replace('CLASSPATH=$APP_HOME/lib', 'CLASSPATH=$APP_HOME/conf/:$APP_HOME/lib')
        }
    }

    dockerDistTar {
        instruction {"RUN mkdir -p /${applicationName}/logs"}
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.5'
    }
}

dependencies {
    compile project(':common')
}